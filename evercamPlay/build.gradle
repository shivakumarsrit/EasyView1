buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId "io.evercam.androidapp"
        minSdkVersion 14
        targetSdkVersion 23

        //Enable multiDex, but the better solution is to reduce method number
        multiDexEnabled = true

        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    //Increase memory to avoid build error 'OutOfMemoryError: GC overhead limit exceeded'
    dexOptions {
        javaMaxHeapSize "2048M"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //Exclude files that causes duplicate files error when packaging
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
    }

    //Deal with "Method ... not mocked." error
    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    maven { url 'https://dl.bintray.com/intercom/intercom-maven' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':showcaseView')
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:percent:23.3.0'
    compile 'joda-time:joda-time:2.9.3'
    // Keep using Picasso to load thumbnail in live view, may replace it with volley later
    compile 'com.squareup.picasso:picasso:2.5.2'
    // Use Volley to load thumbnail in camera list instead of Picasso for better error handling
    compile 'com.android.volley:volley:1.0.0'
    compile 'io.keen:keen-client-api-android:2.2.0@aar'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile "com.mixpanel.android:mixpanel-android:4.8.5"
    compile 'com.google.android.gms:play-services-analytics:8.4.0'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    //Material floating action button
    compile 'com.getbase:floatingactionbutton:1.10.1'
    //The scroll view library to show/hide toolbar
    compile 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    //Required for observablescrollview
    compile 'com.nineoldandroids:library:2.4.0'
    //PhotoView library for browsing saved images
    compile 'com.commit451:PhotoView:1.2.4'
    //Enable Multidex
    compile 'com.android.support:multidex:1.0.1'
    //Use super toast library for extra long Toast
    compile 'com.github.johnpersano:supertoasts:1.3.4.1@aar'
    compile('org.simpleframework:simple-xml:2.7.1') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile 'com.badoo.mobile:android-weak-handler:1.1'

    //Test dependencies
    testCompile 'junit:junit:4.12'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    compile('com.github.afollestad.material-dialogs:core:0.8.5.1@aar') {
        transitive = true
    }
    compile('io.intercom.android:intercom-sdk:1.+@aar') {
        transitive = true
    }

    //WebSocket for JPG live view
    compile 'com.github.eoinsha:JavaPhoenixChannels:master-SNAPSHOT'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.7'
    compile 'de.hdodenhof:circleimageview:2.0.0'

    compile 'io.reactivex:rxandroid:1.1.0'
    // Because RxAndroid releases are few and far between, it is recommended to
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.3'
}

def props = new Properties()

props.load(new FileInputStream(rootProject.file("release.properties")))

android.signingConfigs.release.storeFile rootProject.file(props.keyStore)

android.signingConfigs.release.storePassword props.keyStorePassword

android.signingConfigs.release.keyAlias props.keyAlias

android.signingConfigs.release.keyPassword props.keyAliasPassword